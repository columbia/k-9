apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

if (!rootProject.optimizeForDevelopment) {
    apply from: '../gradle/plugins/checkstyle-android.gradle'
    apply from: '../gradle/plugins/findbugs-android.gradle'
}

if (rootProject.testCoverage) {
    apply plugin: 'jacoco'
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'org.apache.james:apache-mime4j-core:0.8.1'
    compile 'org.apache.james:apache-mime4j-dom:0.8.1'
    compile 'commons-io:commons-io:2.4'
    compile 'com.jcraft:jzlib:1.0.7'
    compile 'com.beetstra.jutf7:jutf7:1.0.0'
    compile "com.android.support:support-annotations:${androidSupportLibraryVersion}"
    compile "com.jakewharton.timber:timber:${timberVersion}"
    compile group: 'com.google.guava', name: 'guava', version: '23.5-android'

    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.madgag.spongycastle:pg:1.51.0.0'

    testCompile "org.jetbrains.kotlin:kotlin-stdlib-jre7:${kotlinVersion}"
    testCompile "com.squareup.okio:okio:${okioVersion}"
    testCompile "org.robolectric:robolectric:${robolectricVersion}"
    testCompile "junit:junit:${junitVersion}"
    testCompile "com.google.truth:truth:${truthVersion}"
    testCompile "org.mockito:mockito-core:${mockitoVersion}"
}

android {
    compileSdkVersion androidCompileSdkVersion.toInteger()
    buildToolsVersion androidBuildToolsVersion

    defaultConfig {
        minSdkVersion 15

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters "arm64-v8a"
            //abiFilters "armeabi-v7a"
        }
    }

    externalNativeBuild {
        cmake {
            path "../openssl/arm64-v8a/CMakeLists.txt"
            //path "../openssl/armeabi-v7a/CMakeLists.txt"
        }
    }

    // for using Apache HTTP Client
    useLibrary 'org.apache.http.legacy'

    buildTypes {
        debug {
            testCoverageEnabled rootProject.testCoverage
        }
    }

    lintOptions {
        abortOnError false
        lintConfig file("$rootProject.projectDir/config/lint/lint.xml")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
    }
}
